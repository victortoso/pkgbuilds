# $Id$
# Maintainer: Victor Toso <me@victortoso.com>

pkgname=virt-viewer-git
pkgver=6.0.r14.1b89efa
pkgrel=1
pkgdesc="A lightweight interface for interacting with the graphical display of virtualized guest OS."
depends=('gtk-vnc' 'libvirt' 'spice-gtk-git' 'desktop-file-utils' 'libvirt-glib')
makedepends=('intltool' 'spice-protocol-git')
url="http://virt-manager.org/"
arch=('i686' 'x86_64')
license=('GPL')
source=("${pkgname}::git+https://pagure.io/virt-viewer.git")
sha256sums=('SKIP')
conflicts=('virt-viewer')

pkgver() {
  cd "${pkgname}"
  git describe --long | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  ./autogen.sh --prefix=/usr \
      --with-gtk=3.0 \
      --with-gtk-vnc \
      --with-spice-gtk \
      --with-libvirt \
      --without-ovirt \
      --disable-update-mimedb
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR=$pkgdir install
}
