# $Id$
# Maintainer: Victor Toso <me@victortoso.com>

pkgname=libva-git
pkgver=2.0.0.r44.68a4bef
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
url='https://01.org/linuxmedia/vaapi'
license=('MIT')
depends=('glibc' 'libdrm' 'libgl' 'libx11' 'libxext' 'libxfixes' 'wayland')
makedepends=('mesa')
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
provides=('libva=${pkgver}' 'libva-drm.so' 'libva-glx.so'
          'libva-wayland.so' 'libva-x11.so' 'libva.so')
source=("${pkgname}::git+https://github.com/01org/libva.git")
conflicts=('libva')
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  git describe --long | sed 's/^libva.//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  cd "${pkgname}"

  ./autogen.sh \
    --prefix='/usr'
  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libva
}

# vim: ts=2 sw=2 et:
